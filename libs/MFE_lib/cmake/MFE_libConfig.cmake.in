@PACKAGE_INIT@

macro(import_targets type)
	if(NOT EXISTS "${CMAKE_CURRENT_LIST_DIR}/MFE_lib-${type}-targets.cmake")
		set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
			"MFE_lib ${type} libraries not found")
		set(${CMAKE_FIND_PACKAGE_NAME}_FOUND OFF)
		return()
	endif()
	
	include("${CMAKE_CURRENT_LIST_DIR}/MFE_lib-${type}-targets.cmake")
endmacro()

if(NOT TARGET MFE_lib::MFE_lib)
	set(type "")
	
	if(DEFINED MFE_LIB_SHARED_LIBS)
		if(MFE_LIB_SHARED_LIBS)
			set(type "shared")
		else()
			set(type "static")
		endif()
	elseif(BUILD_SHARED_LIBS AND EXISTS 
	"${CMAKE_CURRENT_LIST_DIR}/MFE-lib-shared-targets.cmake")
		set(type "shared")
	elseif(EXISTS "${CMAKE_CURRENT_LIST_DIR}/MFE_lib-static-targets.cmake")
		set(type "static")
	else() 
		set(type "shared")
	endif()
	
	import_targets(${type})
	check_required_components(MFE_lib)
	message("-- FOUND ${type} (version ${${CMAKE_FIND_PACKAGE_NAME}_VERSION})")
endif()